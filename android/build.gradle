apply plugin: 'com.android.application'

def orx_android_folder = file("$System.env.ORX_DIR/code").exists()?"$System.env.ORX_DIR/code" :
								   "$System.env.ORX_DIR/dev-android"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.orx_project.orx_template"
        minSdkVersion 10
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
        jniLibs.srcDirs = ['src/libs']
    } 
}

repositories {
    jcenter()
    flatDir {
        dirs "$orx_android_folder/demo/android/app/aars"
    }
}

task ndkBuild(type:Exec) {
    workingDir 'src'
    commandLine 'ndk-build'
    environment NDK_MODULE_PATH: orx_android_folder
    args "-j4"
}

task runDebug(type:Exec) {
   commandLine 'adb'
   args "shell", "am", "start", "-n", "org.orx_project.orx_template/.OrxTemplate"
   dependsOn build, "installDebug"
}

preBuild.dependsOn ndkBuild

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:20.0.0'
    compile(name:'orx-lib', ext:'aar')
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

